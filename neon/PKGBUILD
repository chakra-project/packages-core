# $Id: PKGBUILD 75397 2010-04-01 12:55:26Z ibiru $
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.28.6
pkgrel=4
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gnutls' 'heimdal' 'zlib' 'expat' 'db')
url="http://www.webdav.org/neon/"
source=(http://www.webdav.org/neon/${pkgname}-${pkgver}.tar.gz)
md5sums=('252578ed555552b71d15909641484951')
options=(libtool)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-ssl=gnutls  --with-expat --enable-shared --disable-static
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
