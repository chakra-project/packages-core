#
# Chakra Packages for Chakra, part of chakra-project.org
#
# maintainer (i686): Phil Miller <philm[at]chakra-project[dog]org>
# maintainer (x86_64): Manuel Tortosa <manutortosa[at]chakra-project[dot]org>

# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.70
_pkgverdm=1.02.52
_pkgverlvm=${pkgver}
pkgrel=1.1
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
groups=('base')
conflicts=('mkinitcpio<0.5.99')
changelog=ChangeLog
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz
        lvm2_install
        lvm2_hook)
md5sums=('39c26d5a821754cc347d7a19e82d4a24'
         'f781c9f5dd4f9934952c687b73d26a18'
         '95bed25bdbc2c2d8500e7a07aeff7f65')
sha1sums=('6f88114e72204feee491befd5f0e203036b90f11'
          '583424bb0a6f89ffe4e4ea446b912f0fedac7050'
          '0dc0c0d620f634c4d078afdb78e3bae15ccf957b')

build() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  sed -i 's|/usr/bin/tr|/bin/tr|' scripts/lvmdump.sh
  unset LDFLAGS
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
    --includedir=/usr/include --with-usrlibdir=/usr/lib \
    --enable-pkgconfig --enable-readline --enable-dmeventd --enable-cmdlib --enable-applib \
    --with-udevdir=/lib/udev/rules.d/ --enable-udev_sync --enable-udev_rules
  make
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'udev')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_device-mapper
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper>=${pkgver}" 'udev' 'readline')
  conflicts=('lvm' 'mkinitcpio<0.5.99')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_lvm2
  # install applib
  cd liblvm
  make DESTDIR="${pkgdir}" install
  cd ..
  # /etc directories
  install -d "${pkgdir}"/etc/lvm/{archive,backup}
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/lib/initcpio/hooks/lvm2"
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/lib/initcpio/install/lvm2"
}
