# $Id: PKGBUILD 59376 2009-11-22 21:18:19Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Konstantinos Pachnis <kpachnis@mes-direct.com>

pkgname=cmake
pkgver=2.8.0
pkgrel=1
pkgdesc="A cross-platform open-source make system"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.cmake.org"
depends=('expat' 'curl' 'gcc-libs' 'shared-mime-info')
#makedepends=('qt')
#optdepends=('qt: cmake-gui')
install=cmake.install
source=("http://www.cmake.org/files/v2.8/$pkgname-$pkgver.tar.gz")
md5sums=('3b3679b8a6afaedc38a8c15dd7ff4fcf')

build() {
	cd $srcdir/$pkgname-$pkgver
	./bootstrap --prefix=/usr \
		--mandir=/share/man \
		--docdir=/share/doc/cmake \
		--system-libs \
		--parallel=2

		#--qt-gui \

	make || return 1
	make DESTDIR=$pkgdir install

	vimpath="$pkgdir/usr/share/vim/vim72"
	install -D -m644 Docs/cmake-indent.vim $vimpath/indent/cmake-indent.vim
	install -D -m644 Docs/cmake-syntax.vim $vimpath/syntax/cmake-syntax.vim
	install -D -m644 Docs/cmake-mode.el $pkgdir/usr/share/emacs/site-lisp/cmake-mode.el
	install -D -m644 Copyright.txt $pkgdir/usr/share/licenses/$pkgname/Copyright.txt	
}
