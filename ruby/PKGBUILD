# $Id: PKGBUILD 65257 2010-01-26 03:09:25Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=1.9.1_p378
_pkgver=1.9.1-p378
pkgrel=3
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.ruby-lang.org/en/"
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline') #'tk'
source=(ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2) 
options=('!emptydirs')
md5sums=('5922459622a23612eb9b68a3586cb5f8')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr --enable-shared --enable-pthread || return 1
  make || return 1
}

package_ruby()
{
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
#   optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install || return 1  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}

package_ruby-docs()
{
  pkgdesc="Documentation files for ruby"
  #arch=('any')
  depends=('ruby')

  cd "${srcdir}/${pkgname}-${_pkgver}"  
  make -j1 DESTDIR="${pkgdir}" install-doc || return 1
}
