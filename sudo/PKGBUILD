# $Id: PKGBUILD 77567 2010-04-15 22:56:58Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sudo
pkgver=1.7.2p6
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
source=(ftp://ftp.sudo.ws/pub/sudo/$pkgname-$pkgver.tar.gz 
        sudo.pam)
options=('!libtool' '!makeflags' 'force')
md5sums=('c4f1a43e8ba94f6bf06d2211442148c4'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd $srcdir/$pkgname-$pkgver || return 1

  ./configure --prefix=/usr --with-pam --libexecdir=/usr/lib \
    --with-env-editor --with-all-insults --with-logfac=auth || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver || return 1

  make DESTDIR=$pkgdir install || return 1
  install -Dm644 $srcdir/sudo.pam $pkgdir/etc/pam.d/sudo || return 1

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/sudo/LICENSE || return 1
}
